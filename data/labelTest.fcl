// FCL file for PLACE test logic
// Note that ';' terminates a FCL line, so any text on a line
// after a ';' is ignored.
// Also, inside a FUZZIFY or DEFUZZIFY block, extra lines that
// don't start with a keyword are ignored.
// Other than that, any line that starts with '//' is ignored.
// Any line that starts with '(*' is ignored.
//
// Problem: Doesn't recognize FCL statements that span lines,
//          except for RULE which can span lines.
// Problem: Doesn't recognize (* ... *) as comment
// 
FUNCTION_BLOCK PLACE_1
// input variables
VAR_INPUT
	TONNAGE      : REAL;
	CRUISE_DEP   : REAL;
	AGE_PRED     : REAL;
	NUM_PORTS    : REAL;
	TABLE_2_VEL  : REAL;
        MODEL_VEL    : REAL;
        SHIP_DENSITY : REAL;
END_VAR
// output variables
VAR_OUTPUT
	assessment : REAL;
END_VAR
// Fuzzification of input variables
FUZZIFY TONNAGE
	TERM very_low := (1000, 1)   (5000, 0);
        TERM low      := (1000, 0)   (5000, 1)   (50000, 1)  (55000, 0);
	TERM medium   := (50000, 0)  (60000, 1)  (250000, 1) (260000, 0);
	TERM large    := (250000, 0) (260000, 1) (800000, 1) (900000, 0);
        TERM extreme  := (800000, 0) (900000, 1);
END_FUZZIFY
FUZZIFY CRUISE_DEP
	TERM low    := (40, 1) (50, 0);
	TERM medium := (40, 0) (50, 1) (200, 1) (250, 0);
	TERM high   := (200,0) (250,1);
END_FUZZIFY
FUZZIFY AGE_PRED
        // Note that this is a year, and rectangular (non-fuzzy)
        TERM pre_1960  := (1800, 0) (1800, 1) (1960, 1) (1960, 0);
	TERM 1960_2000 := (1961, 0) (1961, 1) (1999, 1) (1999, 0);
        TERM post_2000 := (2000, 0) (2000, 1) (2020, 1) (2020, 0);
END_FUZZIFY
FUZZIFY NUM_PORTS
        // Note that this is rectangular (non-fuzzy)
	TERM less_than_five    := (0, 0) (0, 1) (5,  1) (5,  0);
	TERM six_to_fifteen    := (6, 0) (6, 1) (15, 1) (15, 0);
        TERM more_than_fifteen := (16,0) (16,1) (500,1) (500,0);
END_FUZZIFY
FUZZIFY TABLE_2_VEL
	TERM variable := (0, 1) (0.3, 0);
	TERM weak     := (0, 0) (0.3, 1) (1, 1) (1.3, 0);
	TERM strong   := (1, 0) (1.3, 1) (2.5, 1) (3, 0);
	TERM extreme  := (2.5, 0) (3, 1);
END_FUZZIFY
FUZZIFY MODEL_VEL
	TERM variable := (0, 1) (0.3, 0);
	TERM weak     := (0, 0) (0.3, 1) (1, 1) (1.3, 0);
	TERM strong   := (1, 0) (1.3, 1) (2.5, 1) (3, 0);
	TERM extreme  := (2.5, 0) (3, 1);
END_FUZZIFY
FUZZIFY SHIP_DENSITY
        // 
	TERM less_than_ten    := (0, 1) (10, 0);
	TERM eleven_to_thirty := (5, 0) (10, 1) (30, 1) (31, 0);
	TERM more_than_thirty := (30, 0) (31, 1);
END_FUZZIFY
// Defuzzification of output variables 
DEFUZZIFY assessment
	  // singleton output terms
//	  TERM ignore       := 0;
//	  TERM low          := 33.3;
//	  TERM significant  := 66.6;
//	  TERM important    := 99.9;
	  // rectangular output terms
//	  TERM ignore       := (0,  0) (0,  1) (20, 1) (20,   0);
//	  TERM low          := (20, 0) (20, 1) (50, 1) (50,   0);
//	  TERM significant  := (50, 0) (50, 1) (80, 1) (80,   0);
//	  TERM important    := (80, 0) (80, 1) (100, 1) (100, 0);
	  // Ramp, Triangle and Trapezoidal output terms
	  TERM ignore       := (10, 1)  (20, 0);
	  TERM low          := (10, 0)  (10, 1)  (40, 1) (40, 0);
	  TERM significant  := (30, 0)  (40, 1)  (80, 1) (90, 0);
	  TERM important    := (80, 0)  (90, 1);
	  // Defuzzification parameters
	  ACCU: MAX;
	  METHOD: COG;
	  DEFAULT:= 0;
	  RANGE:= (0, 100);
END_DEFUZZIFY
RULEBLOCK No1
	  AND : MIN;
	  OR  : MAX;
	  ACT : MIN;
//----------------------------------------------------------------
	  RULE 0: IF TONNAGE IS large AND AGE_PRED IS pre_1960
		  THEN assessment IS significant;
//----------------------------------------------------------------
	  RULE 1: IF TONNAGE IS extreme AND AGE_PRED IS pre_1960
		  THEN assessment IS important;
//----------------------------------------------------------------
	  RULE 2: IF TONNAGE IS very_low AND AGE_PRED IS post_2000
		  THEN assessment IS ignore;
//----------------------------------------------------------------
	  RULE 3: IF TONNAGE IS medium AND AGE_PRED IS post_2000
                  THEN assessment IS low;
//----------------------------------------------------------------
	  RULE 4: IF TONNAGE IS medium AND AGE_PRED IS 1960_2000
                  THEN assessment IS significant;
//----------------------------------------------------------------
	  RULE 5: IF TONNAGE IS large AND TABLE_2_VEL IS strong
		  THEN assessment IS significant;
//----------------------------------------------------------------
	  RULE 6: IF TONNAGE IS extreme
		  THEN assessment IS important;
//----------------------------------------------------------------
	  RULE 7: IF TONNAGE IS extreme AND TABLE_2_VEL IS extreme
		  THEN assessment IS important;
//----------------------------------------------------------------
	  RULE 8: IF TONNAGE IS large AND CRUISE_DEP IS high
		  THEN assessment IS important;
//----------------------------------------------------------------
	  RULE 9: IF TONNAGE IS extreme AND CRUISE_DEP IS high
		  THEN assessment IS important;
//----------------------------------------------------------------
	 RULE 10: IF NUM_PORTS IS more_than_fifteen
		  THEN assessment IS important;
//----------------------------------------------------------------
	 RULE 11: IF TABLE_2_VEL IS extreme AND NUM_PORTS IS more_than_fifteen
		  THEN assessment IS important;
//----------------------------------------------------------------
//	 RULE 12: IF SHIP_DENSITY IS less_than_ten
//		  THEN assessment IS low;
//----------------------------------------------------------------
//	 RULE 13: IF SHIP_DENSITY IS eleven_to_thirty
//		  THEN assessment IS signficant;
//----------------------------------------------------------------
//	 RULE 14: IF SHIP_DENSITY IS more_than_thirty
//		  THEN assessment IS important;
//----------------------------------------------------------------
END_RULEBLOCK
END_FUNCTION_BLOCK
